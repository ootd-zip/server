plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
}

// dev일 때만 checkStyle 빌드에 포함
if (project.findProperty('env') == 'dev') {
    apply plugin: 'checkstyle'

    checkstyle {
        configFile = file("${rootDir}/config/naver-checkstyle-rules.xml")
        configProperties = ["suppressionFile": "${rootDir}/config/naver-checkstyle-suppressions.xml"]
    }
}

group = 'zip.ootd'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // Jackson Java 8 Date/Time 지원
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'
    // 이미지 리사이즈
    implementation 'net.coobird:thumbnailator:0.4.20'
    // 파일 업로드
    implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
    implementation 'commons-io:commons-io:2.6'
    implementation 'commons-fileupload:commons-fileupload:1.4'

    // Query DSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    // MariaDB 연결
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    // Redis 연결: Redisson
    implementation 'org.redisson:redisson-spring-boot-starter:3.31.0'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.mockito:mockito-core:3.11.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.11.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    // Test DB
    testRuntimeOnly 'com.h2database:h2'
    testImplementation 'com.github.codemonstur:embedded-redis:1.4.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Querydsl 설정
def querydslDir = "$buildDir/generated/querydsl"
// main 빌드 경로에 Querydsl 경로 추가
sourceSets.main.java.srcDirs += [querydslDir]
tasks.withType(JavaCompile).configureEach {
    doFirst {
        delete fileTree(querydslDir)
    }
    // Querydsl QClass 파일 생성 경로 지정
    options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}
